#!/usr/bin/env cbmbasic

10 OPEN 1,1,0, "input"
11 X=0 : Y=0 : DX=1 : DY=0

20 N=0
21 GET#1, C$
22 GET#1, D$
23 IF ASC(D$)=10  THEN GOTO 30
24 IF ASC(D$)=199 THEN GOTO 90
25 N=N*10+ASC(D$)-48
26 GOTO 22

30 REM PRINT C$,N,X,Y,DX,DY
31 IF C$="N" THEN Y=Y-N : GOTO 20
32 IF C$="S" THEN Y=Y+N : GOTO 20
33 IF C$="W" THEN X=X-N : GOTO 20
34 IF C$="E" THEN X=X+N : GOTO 20
35 IF C$="F" THEN X=X+DX*N : Y=Y+DY*N : GOTO 20
36 IF C$="L" THEN N=360-N : GOTO 40
37 IF C$="R" THEN GOTO 40
39 PRINT "BAD C",C$ : GOTO 20

40 IF N=90  THEN TMP=DX : DX=-DY : DY= TMP : GOTO 20
41 IF N=270 THEN TMP=DX : DX= DY : DY=-TMP : GOTO 20
42 IF N=180 THEN DX=-DX : DY=-DY : GOTO 20
49 PRINT "BAD ROT",C$ : GOTO 20

90 IF X<0 THEN X=-X
91 IF Y<0 THEN Y=-Y
92 PRINT X+Y
